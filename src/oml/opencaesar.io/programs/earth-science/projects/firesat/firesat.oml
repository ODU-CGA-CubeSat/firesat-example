@dc:title "FireSat Project"
@dc:^description "Defines the top-level project work breakdown structure"
description <http://opencaesar.io/programs/earth-science/projects/firesat/firesat> with # as firesat-project {
	
	extends <http://opencaesar.io/programs/earth-science/earth-science>

	extends <http://opencaesar.io/programs/earth-science/projects/firesat/systems/pld/pld>

	extends <http://opencaesar.io/programs/earth-science/projects/firesat/systems/spc/spc>
		
	extends <http://opencaesar.io/organizations/jpl>

	// FireSat Project

	@rdfs:label "FireSat Project"
	ref ci earth-science:FireSatProject [
		project:authorizes WP05
		project:authorizes WP06
	]
		
		
	// Labels can be used here to define aliases for the work packages that might express an internal
	// identifier such as an account code. Within an organization you might prefer to define a more specific attribute
	// to make the association to an accounting system more explicit. We don't do that here because we don't want to
	// prescribe how the organization distributes funds. The WBS is meant only to express how authority is
	// delegated within the project so that it is clear who has the authority to say what.
	
	// A real project would likely have other top-level work packages for management, system engineering, ground system or mission operations,
	// launch system, and others. Here, we are mainly focused on the design of the spacecraft so we omit the work packages responsible for
	// those other parts of the project and its mission.
	
	// WP05 Work Package

	@rdfs:label "WP05"
	ci WP05 : project:WorkPackage [
		project:supplies pld:Payload
		project:isResponsibilityOf jpl:JPL
	]

	// WP06 Work Package

	@rdfs:label "WP06"
	ci WP06 : project:WorkPackage [
		project:supplies spc:Spacecraft
		project:isResponsibilityOf jpl:JPL
	]

} 